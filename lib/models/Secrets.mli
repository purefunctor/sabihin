(** Represents a {!User}'s secrets. *)

type t = {
  user_id : int32;  (** References a {!User.t.id}. *)
  client_random_value : bytes;
      (** A random value generated by a CSPRNG in the client.

          Typically 16 bytes in length, this is used as part of the salt
          for deriving the bits in the "[DerivedKey]". *)
  encrypted_master_key : bytes;
      (** The symmetric key used to encrypt the {!encrypted_protection_key}
          and {!encrypted_verification_key}. *)
  master_key_iv : bytes;
      (** The [iv] used to encrypt the {!encrypted_master_key}. *)
  encrypted_protection_key : bytes;
      (** The private key used to decrypt the "[EphemeralKey]" that's passed
          alongside a message. *)
  protection_key_iv : bytes;
      (** The [iv] used to encrypt the {!encrypted_protection_key}.  *)
  exported_protection_key : bytes;
      (** The public key used to encrypt the "[EphemeralKey]" that's passed
          alongside a message. *)
  encrypted_verification_key : bytes;
      (** The private key used to produce the signature that's passed alongside
          a message. *)
  verification_key_iv : bytes;
      (** The [iv] used to encrypt the {!encrypted_verification_key}. *)
  exported_verification_key : bytes;
      (** The public key used to verify the signature that's passed alongside
          a message. *)
}

val create_table :
  (module Rapper_helper.CONNECTION) ->
  (unit, [> Caqti_error.call_or_retrieve ]) result Lwt.t
(** [create_table db] creates the [secrets] table in the [db] provided it does
    not exist. *)

val get_by_user_id :
  user_id:int32 ->
  (module Rapper_helper.CONNECTION) ->
  (t option, [> Caqti_error.call_or_retrieve ]) result Lwt.t
(** [get_by_user_id ~user_id db] obtains a {!Secrets.t} using an [~user_id]. *)

val get_by_username :
  username:string ->
  (module Rapper_helper.CONNECTION) ->
  (t option, [> Caqti_error.call_or_retrieve ]) result Lwt.t
(** [get_by_username ~username db] obtains a {!Secrets.t} using a [~username]. *)

val insert :
  user_id:int32 ->
  client_random_value:bytes ->
  encrypted_master_key:bytes ->
  master_key_iv:bytes ->
  encrypted_protection_key:bytes ->
  protection_key_iv:bytes ->
  exported_protection_key:bytes ->
  encrypted_verification_key:bytes ->
  verification_key_iv:bytes ->
  exported_verification_key:bytes ->
  (module Rapper_helper.CONNECTION) ->
  (unit, [> Caqti_error.call_or_retrieve ]) result Lwt.t
(** [insert ... db] inserts a new row in the [secrets] table. *)
